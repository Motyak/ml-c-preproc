#!/usr/bin/perl
use strict;
use warnings;

my @stack;

sub ml_comment {
    my ($msg) = @_;
    my $ML_COMMENT_WIDTH = 80;
    $msg = "=== mlcpp: ${msg} ===";
    my $comment = "\"" . $msg . "=" x ($ML_COMMENT_WIDTH - 2 - length($msg)) . "\"";
    return $comment;
}

while (my $line = <>) {
    chomp($line); # remove newline character

    # add Monlang comment on inclusion
    if ($line =~ /^# [1-9][0-9]* "(.*)" 1$/) {
        my $include_name = $1;
        push(@stack, $include_name);
        my $msg = "BEGIN ${include_name}";
        my $comment = ml_comment($msg);
        print("${comment}\n");
    }

    # add Monlang comment when returning from inclusion
    elsif ($line =~ /^# [1-9][0-9]* "(.*)" 2$/) {
        @stack || die;
        my $back_to = $1;
        my $popped = pop(@stack);
        my $stack = @stack;
        my $msg = "END ${popped} (" . ($stack == 0? "finally " : "") . "back to ${back_to})";
        my $comment = ml_comment($msg);
        print("${comment}\n");
    }

    # discard other linemarkers generated by cpp
    elsif ($line =~ /^# \d/) {
        ;
    }

    # discard some empty lines generated by cpp
    elsif ($line =~ /^ +$/) {
        ;
    }

    else {
        print("${line}\n")
    }
}
